apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


// keystore file properties
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))


android {
    compileSdkVersion rootProject.ext.compileSdkVersion as Integer
    buildToolsVersion rootProject.ext.buildToolsVersion as String
    defaultConfig {
        applicationId rootProject.ext.applicationId as String
        minSdkVersion rootProject.ext.minSdkVersion as Integer
        targetSdkVersion rootProject.ext.targetSdkVersion as Integer
        versionCode rootProject.ext.versionCode as Integer
        versionName rootProject.ext.versionName as String
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner as String
        vectorDrawables.useSupportLibrary = true

        // Enable multiDex support.
        multiDexEnabled true

        // BUILD CONFIG FIELDS
        buildConfigField 'String', 'BASE_URL', '"https://www.themealdb.com/api/json/v1/1/"'

        viewBinding {
            enabled = true
        }
    }

    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {

        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
            //signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:multidex:$multidexLibVersion"
    implementation "android.arch.lifecycle:extensions:2.2.5"

    //UI
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "com.github.adroitandroid:ChipCloud:$chipCloudLibVersion"

    //Image loader
    implementation("io.coil-kt:coil:0.11.0")

    //Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //API Service
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibVersion"
    implementation "com.github.akarnokd:rxjava3-retrofit-adapter:3.0.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation "com.squareup.retrofit2:converter-moshi:$converter_moshi_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"

    //DB
    implementation "android.arch.persistence.room:runtime:$roomCompilerLibVersion"
    kapt "android.arch.persistence.room:compiler:$roomCompilerLibVersion"

    //Dagger
    implementation "com.google.dagger:dagger:2.27"
    kapt "com.google.dagger:dagger-compiler:2.27"

    //RX
    implementation "io.reactivex.rxjava3:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava3:rxandroid:$rxandroid_version"

    debugImplementation "com.amitshekhar.android:debug-db:$debugDbVersion"
    testImplementation 'junit:junit:4.12'
}
